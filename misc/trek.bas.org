   5 Y=2999;IN."DO YOU WANT A DIFFICULT GAME?  ","(Y OR N)"A
  10 PR."STARDATE 3200:  YOUR MISSION IS ",;IFA=YY=999 
  15 K=0,B=0,D=30;F.I=0TO63;J=R.(99)<5,B=B+J
  20 M=R.(Y),M=(M<209)+(M<99)+(M<49)+(M<24)+(M<9)+(M<2),K=K+M
  25 @(I)=-100*M-10*J-R.(8);N.I;IF(B<2)+(K<4)G.15
  30 PR."TO DESTROY ",#1,K," KLINGONS IN 30 STARDATES. 
  35 PR."THERE ARE ",#1,B," STARBASES.";GOS.160;C=0,H=K 
  40 U=R.(8),V=R.(8),X=R.(8),Y=R.(8)
  45 F.I=71TO152;@(I)=0;N.I;@(8*X+Y+62)=4,M=A.(@(8*U+V-9)),N=M/100
  50 I=1;IFNF.J=1TON;GOS.165;@(J+134)=300,@(J+140)=S,@(J+146)=T;N.J
  55 GOS.175;M=M-100*N,I=2;IFM/10GOS.165
  60 M=M-M/10*10,I=3;IFMF.J=1TOM;GOS.165;N.J
  65 GOS.145;GOS.325;IFKG.95
  70 PR.;PR."MISSION ACCOMPLISHED.";IFD<3PR."BOY, YOU BARELY MADE IT.  
  75 IFD>5PR."GOOD WORK...";IFD>9PR."FANTASTIC!";IFD>13PR."UNBELIEVABLE! 
  80 D=30-D,I=H*100/D*10;PR.#1,H," KLINGONS IN ",D," STARDATES. (",I,") 
  85 J=100*(C=0)-5*C;PR.#1,C," CASUALTIES INCURRED. (",J,") 
  90 PR."YOUR SCORE:",I+J;G.110 
  95 IFD<0PR."IT'S TOO LATE, THE FEDERATION HAS BEEN CONQUERED.";G.110 
 100 IFE>=0G.120
 105 PR."ENTERPRISE DESTROYED";IFH-K>9PR."BUT YOU WERE A GOOD MAN 
 110 Y=987;PR.;IN."ANOTHERE GAME?  ","(Y OR N)"A;IFA=YG.5 
 115 PR."GOOD BYE.";STOP 
 120 S=220,G=180,L=200,P=260,R=420,W=465,T=555;IN."CAPTAIN"A
 125 IF(A=S)+(A=G)+(A=L)+(A=P)+(A=R)+(A=W)+(A=T)G.A
 130 PR."R=REPORT       S=SR. SENSOR   L=LR. SENSOR 
 135 PR."G=GALAXY MAP   P=PHASER       T=TORPEDO 
 140 PR."W=WARP ENGINE  **PLEASE USE ONE OF THESE COMMANDS***";G.120 
 145 F.I=X-(X>1)TOX+(X<8);F.J=Y-(Y>1)TOY+(Y<8)
 150 IF@(8*I+J+62)#2N.J;N.I;O=0;R.
 155 IFO=0PR.'SULU: "CAPTAIN, WE ARE DOCKED AT STARBASE." 
 160 E=4000,F=10,O=1;F.I=64TO70;@(I)=0;N.I;R.
 165 S=R.(8),T=R.(8),A=8*S+T+62;IF@(A)G.165
 170 @(A)=I;R.
 175 PR."ENTERPRISE IN Q-",#1,U,V," S-",X,Y;R. 
 180 GOS.175;J=2;GOS.375;IFIG.120
 185 PR." OF GALAXY MAP";F.I=0TO7;PR.;PR.#1,I+1,":",;F.J=0TO7;M=@(8*I+J) 
 190 PR.#4,(M>0)*M,;N.J;PR.;N.I;PR."  ",;F.I=0TO7;PR."  ..",;N.I;PR. 
 195 PR."  ",;F.I=1TO8;PR.#4,I,;N.I;PR.;PR.;G.120 
 200 GOS.175;J=3;GOS.375;IFIG.120
 205 PR.;F.I=U-1TOU+1;F.J=V-1TOV+1;M=8*I+J-9,A=0 
 210 IF(I>0)*(I<9)*(J>0)*(J<9)A=A.(@(M)),@(M)=A
 215 PR.#4,A,;N.J;PR.;N.I;G.120 
 220 GOS.175;J=1;GOS.375;IFIG.120
 225 M=8*U+V-9,@(M)=A.(@(M))
 230 PR.;F.I=1TO8;PR.#1,I,;F.J=1TO8;M=@(8*I+J+62);IFM=0PR." .", 
 235 IFM=1PR." K", 
 240 IFM=2PR." B", 
 245 IFM=3PR." *", 
 250 IFM=4PR." E", 
 255 N.J;PR.;N.I;PR." ",;F.I=1TO8;PR.#2,I,;N.I;PR.;G.120 
 260 J=4;GOS.375;IFIG.120
 265 IN." ENERGIZED. ","UNITS TO FIRE"A;IFA<1G.120
 270 IFA>EPR.'SPOCK: "WE HAVE ONLY ',#1,E,' UNITS."';G.120 
 275 E=E-A;IFN<1PR."PHASER FIRED AT EMPTY SPACE.";G.65 
 280 A=A/N;F.M=135TO140;IF@(M)=0G.290
 285 GOS.295;PR.#3,S," UNITS HIT ",;GOS.305 
 290 N.M;G.65
 295 IFA>1090PR."...OVERLOADED..";J=4,@(67)=1,A=9;GOS.375 
 300 I=@(M+6)-X,J=@(M+12)-Y,S=A*30/(30+I*I+J*J)+1;R.
 305 PR."KLINGON AT S-",#1,@(M+6),@(M+12),;@(M)=@(M)-S 
 310 IF@(M)>0PR." **DAMAGED**";R. 
 315 @(M)=0,I=8*U+V-9,J=@(I)/A.(@(I)),@(I)=@(I)-100*J,K=K-1
 320 I=8*@(M+6)+@(M+12)+62,@(I)=0,N=N-1;PR." ***DESTROYED***";R. 
 325 IFN=0R.
 330 PR."KLINGON ATTACK";IFOPR."STARBASE PROTECTS ENTERPRISE";R. 
 335 T=0;F.M=135TO140;IF@(M)=0G.350
 340 A=(@(M)+R.(@(M)))/2;GOS.295;T=T+S,I=@(M+6),J=@(M+12)
 345 PR.#3,S," UNITS HIT FROM KLINGON AT S-",#1,I,J 
 350 N.M;E=E-T;IFE<=0PR."*** BANG ***";R.R
 355 PR.#1,E," UNITS OF ENERGY LEFT.";IFR.(E/4)>TR. 
 360 IF@(70)=0@(70)=R.(T/50+1),J=7;G.375
 365 J=R.(6),@(J+63)=R.(T/99+1)+@(J+63),I=R.(8)+1,C=C+I
 370 PR.'MC COY: "SICKBAY TO BRIDGE, WE SUFFERED',#2,I,' CASUALTIES." 
 375 I=@(J+63);IFJ=1PR."SHORT RANGE SENSOR", 
 380 IFJ=2PR."COMPUTER DISPLAY", 
 385 IFJ=3PR."LONG RANGE SENSOR", 
 390 IFJ=4PR."PHASER", 
 395 IFJ=5PR."WARP ENGINE", 
 400 IFJ=6PR."PHOTON TORPEDO TUBES", 
 405 IFJ=7PR."SHIELD", 
 410 IFI=0R.
 415 PR." DAMAGED, ",#1,I," STARDATES ESTIMATED FOR REPAIR";R. 
 420 PR."STATUS REPORT:";PR."STARDATE",#10,3230-D;PR."TIME LEFT",#7,D 
 425 PR."CONDITION     ",;IFOPR."DOCKED";G.445 
 430 IFNPR."RED";G.445 
 435 IFE<999PR."YELLOW";G.445 
 440 PR."GREEN 
 445 PR."POSITION      Q-",#1,U,V," S-",X,Y;PR."ENERGY",#12,E 
 450 PR."TORPEDOES",#7,F;PR."KLINGONS LEFT",#3,K;PR."STARBASES",#6,B 
 455 F.J=1TO7;IF@(J+63)GOS.375
 460 N.J;G.120
 465 J=5;GOS.375;IFI=0PR. 
 470 IN."SECTOR DISTANCE"W;IFW<1G.120
 475 IFI*(W>2)PR.'CHEKOV: "WE CAN TRY 2 AT MOST, SIR."';G.470 
 480 IFW>91W=91;PR.'SPOCK: "ARE YOU SURE, CAPTAIN?" 
 485 IFE<W*W/2PR.'SCOTTY: "SIR, WE DO NOT HAVE THE ENERGY."';G.120 
 490 GOS.615;IFR=0G.120 
 495 D=D-1,E=E-W*W/2,@(8*X+Y+62)=0 
 500 F.M=64TO70;@(M)=(@(M)-1)*(@(M)>0);N.M 
 505 P=45*X+22,G=45*Y+22,W=45*W;F.M=1TO8;W=W-R;IFW<-22G.525 
 510 P=P+S,G=G+T,I=P/45,J=G/45;IF(I<1)+(I>8)+(J<1)+(J>8)G.530 
 515 IF@(8*I+J+62)=0X=I,Y=J;N.M 
 520 PR."**EMERGENCY STOP**";PR.'SPOCK: "TO ERR IS HUMAN." 
 525 @(8*X+Y+62)=4;GOS.175;G.65 
 530 P=U*72+P/5+W/5*S/R-9,U=P/72,G=V*72+G/5+W/5*T/R-9,V=G/72 
 535 IFR.(9)<2PR."***SPACE STORM***";T=100;GOS.360 
 540 IF(U>0)*(U<9)*(V>0)*(V<9)X=(P+9-72*U)/9,Y=(G+9-72*V)/9;G.45 
 545 PR."**YOU WANDERED OUTSIDE THE GALAXY** 
 550 PR."ON BOARD COMPUTER TAKES OVER, AND SAVED YOUR LIFE";G.40 
 555 J=6;GOS.375;IFIG.120 
 560 IFF=0PR." EMPTY";G.120 
 565 PR." LOADED";GOS.615;IFR=0G.120 
 570 PR."TORPEDO TRACK ",;F=F-1,P=45*X+22,G=45*Y+22;F.M=1TO8 
 575 P=P+S,G=G+T,I=P/45,J=G/45;IF(I<1)+(I>8)+(J<1)+(J>8)G.585 
 580 L=8*I+J+62,W=8*U+V-9,R=@(W)/A.(@(W));PR.#1,I,J," ",;G.585+5*@(L) 
 585 N.M;PR."...MISSED";G.65 
 590 S=R.(99)+280;F.M=135TO140;IF(@(M+6)=I)*(@(M+12)=J)GOS.305
 592 N.M;G.65
 595 B=B-1,@(L)=0,@(W)=@(W)-10*R;PR."STARBASE DESTROYED 
 597 PR.'SPOCK: "I OFTEN FIND HUMAN BEHAVIOUR FASCINATING."';G.65 
 600 PR."HIT A STAR";IFR.(9)<3PR."TORPEDO ABSORBED";G.65 
 605 @(L)=0,@(W)=@(W)-R;IFR.(9)<6PR."STAR DESTROYED";G.65 
 610 T=300;PR."IT NOVAS    ***RADIATION ALARM***";GOS.360;G.65 
 615 IN."COURSE (0-360)"I;IF(I>360)+(I<0)R=0;R.
 620 S=(I+45)/90,I=I-S*90,R=(45+I*I)/110+45;G.625+5*(S<4)*S
 625 S=-45,T=I;R.
 630 S=I,T=45;R.
 635 S=45,T=-I;R.
 640 S=-I,T=-45;R.
