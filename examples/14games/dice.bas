10 REM "DICE from 101 BASIC games"
20 REM "Originally written by Danny Freidus"
30 REM "Ported to Stefan's BASIC in 2021"
40 DIM F(12), P(12)
100 PRINT "THIS PROGRAM SIMULATES THE ROLLING OF A"
110 PRINT "PAIR OF DICE."
120 PRINT "YOU ENTER THE NUMBER OF TIMES YOU WANT THE COMPUTER TO"
130 PRINT "'ROLL' THE DICE.  WATCH OUT, VERY LARGE NUMBERS TAKE"
140 PRINT "A LONG TIME.  IN PARTICULAR, NUMBERS OVER 5000."
200 FOR Q=1 TO 12
210 F(Q)=0
220 NEXT Q
230 P(1)=0
240 FOR Q=2 TO 12
250 IF Q<8 THEN P(Q)=P(Q-1)+1 ELSE P(Q)=P(Q-1)-1
260 NEXT Q
300 INPUT "HOW MANY ROLLS? ", X
310 FOR S=1 TO X
320 R=INT(RND(6)+1)+INT(RND(6)+1)
330 F(R)=F(R)+1
340 NEXT S
350 PRINT
360 PRINT "SPOTS","NUMBER", "EXPECTATION", "DEVIATION"
370 FOR V=2 TO 12
380 PRINT #5, V, #8, F(V), X/36*P(V), ABS(F(V)-X/36*P(V))
390 NEXT V
400 PRINT:PRINT "TRY AGAIN";
410 INPUT Z$
420 IF Z$(1,1)="Y" OR Z$(1,1)="y" THEN 200
500 END
